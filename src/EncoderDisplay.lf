/**
 * Display encoder measurements from the wheels of the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
target uC;

import Display from "lib/Display.lf"
import Encoders from "lib/Encoders.lf"

main reactor {
  display = new Display()
  encoder = new Encoders()

  timer t(0, 1 s)

  reaction(t) -> encoder.trigger {=
    lf_set(encoder.trigger, true);
  =}

  reaction(startup) -> display.line0 {=
    lf_set(display.line0, "ENCODERS:");
  =}

  reaction(encoder.left) -> display.line1 {=
    DisplayString buf;
    snprintf(buf.text, 17, "L: %d", encoder.left->value);
    lf_set(display.line1, buf);
  =}

  reaction(encoder.right) -> display.line2 {=
    DisplayString buf;
    snprintf(buf.text, 17, "R: %d", encoder.right->value);
    lf_set(display.line2, buf);
  =}
}
