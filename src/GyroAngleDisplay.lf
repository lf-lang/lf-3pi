/**
 * Display measurements from the gyroscope on the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a> with integration so the
 * result is in degrees since reset. The gyroscope is provided by an
 * <a href="https://www.pololu.com/file/0J1899/lsm6dso.pdf">ST LMS6DSO inertial measurement
 * unit</a>.
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
target uC;

import Display from "lib/Display.lf"
import GyroAngle from "lib/IMU.lf"

main reactor {
  display = new Display()
  gyro = new GyroAngle()

  timer t(0, 100 ms)

  reaction(t) -> gyro.trigger {=
    lf_set(gyro.trigger, true);
  =}

  reaction(startup) -> display.line0 {=
    lf_set(display.line0, "GYRO ANGLE:");
  =}

  reaction(gyro.x) -> display.line1 {=
    DisplayString buf;
    snprintf(buf.text, 17, "x: %.1f", gyro.x->value);
    lf_set(display.line1, buf);
  =}

  reaction(gyro.y) -> display.line2 {=
    DisplayString buf;
    snprintf(buf.text, 17, "y: %.1f", gyro.y->value);
    lf_set(display.line2, buf);
  =}

  reaction(gyro.z) -> display.line3 {=
    DisplayString buf;
    snprintf(buf.text, 17, "z: %.1f", gyro.z->value);
    lf_set(display.line3, buf);
  =}
}
